# Maintainer: Lukas Heindl <oss.heindl@protonmail.com>
pkgname=dmenu-launcher-dev
pkgver=b659cb8
pkgrel=1
pkgdesc="A simple app launcher using dmenu built from git dev"
arch=('x86_64')
url="https://github.com/atticus-sullivan/dmenu-launcher"
license=('MIT')
makedepends=('git' 'cargo')
optdepends=('dmenu: can also be installed manually')
source=("${pkgname}::git+${url}.git#branch=dev")
sha256sums=('SKIP')

pkgver() {
	cd "${pkgname}"
	git rev-parse --short HEAD
}

prepare() {
	cd "${pkgname}"
	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}"

	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target

	cargo build --frozen --release --all-features
}

check() {
	export RUSTUP_TOOLCHAIN=stable
	cargo test --frozen --all-features
}

package() {
	cd "${pkgname}"
	install -Dm755 "target/release/dmenu-launcher-utils" "$pkgdir/usr/local/bin/dmenu-launcher-utils"
	install -Dm755 "dmenu-launcher.bash"                 "$pkgdir/usr/local/bin/dmenu-launcher"
	install -Dm644 "config.yml"                          "$pkgdir/usr/local/etc/dmenu-launcher.yml"
}
