# Maintainer: Lukas Heindl <oss.heindl@protonmail.com>
pkgname=dmenu-launcher
pkgver=1.1.0
pkgrel=1
pkgdesc="A simple app launcher using dmenu"
arch=('x86_64')
url="https://git.atticus-sullivan.de/wm-tools/dmenu-launcher"
license=('MIT')
makedepends=('git' 'cargo')
conflicts=("dmenu-launcher-dev")
provides=("dmenu-launcher")
optdepends=('dmenu: can also be installed manually')
source=("${pkgname}-${pkgver}::git+ssh://git@git.atticus-sullivan.de:3022/wm-tools/dmenu-launcher.git#tag=${pkgver}")
sha256sums=('SKIP')

prepare() {
	cd "${pkgname}-${pkgver}"
	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}-${pkgver}"

	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target

	cargo build --frozen --release --all-features
}

# check() {
# 	export RUSTUP_TOOLCHAIN=stable
# 	cargo test --frozen --all-features
# }

package() {
	cd "${pkgname}-${pkgver}"
	install -Dm755 "target/release/dmenu-launcher-utils" "$pkgdir/usr/local/bin/dmenu-launcher-utils"
	install -Dm755 "dmenu-launcher.bash"                 "$pkgdir/usr/local/bin/dmenu-launcher"
	install -Dm644 "config.yml"                          "$pkgdir/usr/local/etc/dmenu-launcher.yml"
}
